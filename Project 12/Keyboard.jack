// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    static int keyboardInput;

    /** Initializes the keyboard. */
    function void init() {
        let keyboardInput = 24576;
        return;
    } 

    /**
     * Returns the ASCII code (as char) of the currently pressed key,
     * or 0 if no key is currently pressed.
     * Recognizes all ASCII characters, as well as the following extension
     * of action keys:
     * New line = 128 = String.newline()
     * Backspace = 129 = String.backspace()
     * Left Arrow = 130
     * Up Arrow = 131
     * Right Arrow = 132
     * Down Arrow = 133
     * Home = 134
     * End = 135
     * Page Up = 136
     * Page Down = 137
     * Insert = 138
     * Delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        var int retVal;
        let retVal = Memory.peek(keyboardInput);
        return retVal;
    }

    /**								
     * Reads the next character from the keyboard.
     * waits until a key is pressed and then released, then echoes
     * the key to the screen, and returns the value of the pressed key.
     */
    function char readChar() {
        var char c;
        var char val;

        let c = Keyboard.keyPressed();
        while (c = 0) {
            let c = Keyboard.keyPressed();
        }
        let val = c;

        while (~(c = 0)) {
            let c = Keyboard.keyPressed();
        }

        if ((val < 127) & (val > 31)) {
            do Output.printChar(val);
        }
        return val;
    }

    /**								
     * Prints the message on the screen, reads the next line
     * (until a newline character) from the keyboard, and returns its value.
     */
    function String readLine(String message) {
        var String retVal;
        var char c;

        let retVal = String.new(64); //size of line
        do Output.printString(message);
        while(true) {
            let c = Keyboard.readChar();
            if (c = 128) { // if c is newLine char
                do Output.println();
                return retVal;
            }
            if (c = 129) { // if c is backspace
                do retVal.eraseLastChar();
                do Output.backSpace();
            }
            if ((c < 127) & (c > 31)) {
                do retVal.appendChar(c);
            }
        }
        return retVal;
    }   

    /**								
     * Prints the message on the screen, reads the next line
     * (until a newline character) from the keyboard, and returns its
     * integer value (until the first non numeric character).
     */
    function int readInt(String message) {
        var String str;
        var int retVal;

        let str = Keyboard.readLine(message);
        let retVal = str.intValue();

        str.dispose();
        return retVal;
    }
}
